<!DOCTYPE aesl-source>
<network>


<!--list of global events-->


<!--list of constants-->
<constant value="20" name="SIZE"/>
<constant value="200" name="PERIOD"/>
<constant value="523" name="BASE"/>
<constant value="10" name="FACTOR"/>
<constant value="25" name="DURATION"/>


<!--show keywords state-->
<keywords flag="false"/>


<!--node thymio-II-->
<node nodeId="1" name="thymio-II"># Thymio-II program: Compose music on-the-fly
# Copyright 2013 by Moti Ben-Ari
# CreativeCommons BY-SA 3.0

# Store the left-right acceleration and
#   generate sound from the results

# Constants
#   SIZE        number of samples
#   PERIOD      milliseconds between samples
#   BASE        base frequency for sound
#   FACTOR      factor for change to frequency
#   DURATION    duration of each sound

# Variables
var save[SIZE]    # array to store samples
var index = 0     # array index
var state = 0     # state

# Set timer to PERIOD and zero out the array
call leds.top(0,0,0)
timer.period[1] = PERIOD
call math.fill(save, 0)

# When the center button is released, start sampling
onevent button.center
  when  button.center == 0 do
    if  state == 0 then
      state = 1
  	  call leds.top(32,0,0)
  	elseif  state == 2 then
  	  state = 3
  	  call leds.top(0,0,32)
      call sound.freq(0,1)
    elseif  state == 3 then
      state = 0
      call leds.top(0,0,0)
      call sound.freq(0,-1)
  	end
    index = 0
  end 

# On timer expiration, save a sample
# When all samples are saved, initiate the sounds
onevent timer1
  if  state == 1 then
    if  index &lt; SIZE then
      save[index] = acc[0]
      index++
    else
      state = 2
      call leds.top(0,32,0)
    end
  end

# When a sound is finished, start the next one
onevent sound.finished
  if  state != 3 then
  	return
  end
  if  index &lt; SIZE then
    call sound.freq(BASE+save[index]*FACTOR, DURATION)
    index++
  else
    state = 0
    call leds.top(0,0,0)
  end
</node>


</network>
