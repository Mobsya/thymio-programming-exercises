<!DOCTYPE aesl-source>
<network>


<!--list of global events-->


<!--list of constants-->
<constant value="300" name="MIN"/>
<constant value="450" name="MAX"/>
<constant value="32" name="SPEED"/>
<constant value="500" name="DELTA"/>
<constant value="8" name="COUNT"/>


<!--show keywords state-->
<keywords flag="false"/>


<!--node thymio-II-->
<node nodeId="1" name="thymio-II"># Thymio-II program: odometry
# Copyright 2014 by Moti Ben-Ari
# CreativeCommons BY-SA 3.0

# Constants
#   MIN       minimum motor target speed
#   MAX       maximum motor target speed
#   DELTA     time between samples in milliseconds
#   COUNT     number of samples
#   SPEED     speed of robot per 100 power setting (*10)

var state = 0	  # State variable
var count         # Counter for sampling
var deltaD        # Distance traveled in one sample
var deltaT        # Time of one sample in seconds (*10)
var distance      # Total distance traveled
var target        # Target speed

timer.period[0] = DELTA
deltaT = (DELTA * 10)/ 1000

# Stop the motors and set state to 0
sub stop
    state = 0
    motor.left.target = 0
    motor.right.target = 0

# Set motor target randomly 
sub set_motor
    call math.rand(target)
    target = ((abs target) / 100) + MIN
    if  target > MAX then
      target = MAX
    end
    motor.left.target = target
    motor.right.target = target

# Touch center button to start and stop
onevent button.center
  if  button.center == 0 then
    if  state == 0 then
  	  count = 0
  	  callsub set_motor
      state = 1
    else
      callsub stop
    end
  end

# On timer event, sample speed
# Terminate when COUNT samples have been taken
onevent timer0
  if  state == 0 then
  	return
  end
  if  count > 0 then
    call math.muldiv(
      deltaD,
      (motor.left.speed+motor.right.speed)/2,
      SPEED*deltaT,
      100)
    distance = distance + deltaD
    callsub set_motor
  end
  if count == COUNT then
    callsub stop
    # Remove scaling factors from SPEED and deltaT
    distance = distance / 100
  end
  count++ 	
</node>


</network>
